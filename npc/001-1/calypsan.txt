// Evol scripts.
// Authors:
//    Reid
// Description:
//    Dye seller, she sells and explain how works dye cards.
// Version:
//    2016.019 "Under Construction".
// Variables:
//    ArtisQuests_Fexil
// Quest states:
//    0  -- not started
//    1  -- Lloyd warned about the quest
//    2  -- Fexil explain what he needs
//    3  -- Fexil buy every fur that the pc bring to him

001-1,47,126,0	script	Calypsan#001-1	NPC_CALYPSAN,{

    function explain_dyes {
        speech 5,
            l("Look at your equipment."),
            l("Guess what material it is made from."),
            l("Seek a colorant for that material."),
            l("Cotton, cashmere, leather, ..."),
            l("Zinc-Titanium Oxide, Phthalocyanides, Cadmiums, Lazulite..."),
            l("Colorant is not the yeying color. Some wear out from light,"),
            l("others look great but inflict itai-itai,"),
            l("and others become spotty or wash-away."),
            l("What yoiis should know:"),
            l("1: The type of item yoiis want to dye."),
            l("2: What material it is made from."),
            l("3: Which colorants can dye it."),
            l("4: How to care for it."),
            l("If you aren't careful the item could become ruined.");

        // Need tritan's dialogue (so the first line need to be changed too)
        // Need to explain style cards, and to redirect to Don the blacksmith for stat cards.
        // Style cards change the color or the visual aspect of an item. There are multiple cards that can work on multiple equipment.
        // Each piece of equipment hold a certain amount of possible cards that can be inserted.
        // Ex: Tank Top + Red dye = Red Tank Top
        // Another example, Resa from the light armor shop offer a Brimmed Hat, you can upgrade it with a feather and obtain a feather brimmed hat.
    }


    speech 4,
        l("Yoiis is jealous of rainbow sky?"),
        l("It is possible to yeye your cloth with my beautiful dyes."),
        l("What does yoiis want today?");

    .@fexil = getq(ArtisQuests_Fexil);

    do
    {
        menuint
            rif(.@fexil == 1, l("Have you seen Fexil?")), 0,
            l("[Trade]"), 1,
            l("[Ask how dyes are used]"), 2,
            l("[Ask how dyes are made]"), 3,
            l("[Quit]"), 4;

        switch (@menuret)
        {
            case 0:
                setcamnpc "Fexil#001-1";
                speech 5,
                    l("Oh yeye did, his store is just aside me!"),
                    l("Yoiis should look there.");
                restorecam;

                break;
            case 1:
                closedialog;
                shop "Store#Calypsan001-1";
                close;
            case 2:
                explain_dyes;
                break;
            case 3:
                speech 5,
                    l("Yoiis is not yet ready to make his own dyes."),
                    l("But in the future I might help you to create your own ones.");
                break;
            case 4:
                break;
        }
    } while (@menuret != 4);

    closedialog;
    goodbye;
    close;

OnInit:
    .sex = G_FEMALE;
    .distance = 3;
    end;
}

