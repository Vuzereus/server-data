// Evol scripts.
// Authors:
//    Reid
// Description:
//    Newbie guide for Artis.
// Variables:
//    ArtisQuests_Enora
// Values:
//    0    Default.
//    1    BlackSmith quest delivered.
//    2    Chelios Quest given.
//    3    Chelios Quest done.
//    4    BlackSmith gave the sword.
//    5    Light Armor Shop quest delivered.
//    6    Light Armor Shop gave the cloths.
//    7    Market quest delivered.
//    8    Q'Pid merchant.
//    9    Market gave the potion.
//    10   Hill quest delivered.
//    11   Fluffy killed.

001-1,176,113,0	script	Enora#001-1	NPC_HUMAN_FEMALE_NOOB,{

    function enora_don {
        speech 5,
            l("Don is renowned throughout the archipelagos for his forgery here on Artis."),
            l("It's a great honnor for us at the Legion to hold the security of his commerce."),
            l("If you search for him you should look to the west of this city, it's the first house just after left bridge, you can't miss it!"),
            l("Chelios, his apprentice, practices outside, mostly.");

        return;
    }

    function enora_legion {
        speech 5,
            l("The Legion of Aemil is a group of mercenaries who vowed to protect and serve their neighbors."),
            l("We have various training sessions, and also a board with open tasks for the city and its surroundings."),
            l("It's a good place to earn money, and also help you to travel throughout the archipelagos!"),
            l("Each big city host a Legion chapter, Artis' chapter building is located in the North-West.");

        return;
    }

    function enora_light_armor {
        speech 5,
            l("The light armor shop is ran by Resa, she is part of the Merchant Guild of Artis."),
            l("She is very skilled when it comes to change a no ordinary piece of clothe into a wonderful piece of art!"),
            l("Her shop is on the west part of the city, between the legion building and the library.");

        return;
    }

    function enora_market {
        return;
    }

    function enora_hill {
        speech 5,
            l("Go to the up of the river, and cross the river twice with the two bridges in the North."),
            l("The hill is located in the North-East of Artis.");

        return;
    }

    function enora_memories {
        speech 5,
            l("It's so exciting to meet somebody with amnesia!"),
            l("Do you, like, remember nothing at all? Or do you have SOME memories of your past?"),
            l("What happens when you try to think about it?"),
            l("Does your mind go all fuzzy or is that your head will explode??"),
            l("Try doing that now!");

        switch (select (l("Okay, but there won't be any explosions."),
                        l("Don't get too excited about it...")))
        {
            case 1:
                narrator 5,
                    l("You concentrate and try to summon old memories from within your mind."),
                    l("All you can find is thick mist and the cold hard wood of your raft."),
                    l("A headache hits you and you lose your concentration.");

                speech 4,
                    l("So? You haven't exploded yet! Are you getting anything?");

                select (l("Not really. All I got was a headache..."));

                speech 5,
                    l("It's like your memories are locked away in your head! Cool!");

                break;
            case 2:
                mes "";
                break;
        }

        speech 4,
            l("Ok, sorry. Back to our fluffies.");

        return;
    }

    function enora_first_quest {
        speech 5,
            l("You probably don't have much business in this city as you don't remember who you are."),
            l("So I was going to ask if maybe you would be interested in giving me a hand with a few errands.");

        switch (select (l("I guess so. What's in it for me?"),
                        l("Of course! What do you need?")))
        {
            case 1:
                speech 5,
                    l("Some tasks will help you become aquainted with Artis. Trust me.");
                break;
            case 2:
                mes "";
                break;
        }

        speech 4,
            l("I asked Don our blacksmith to prepare a black iron sword for this morning, he should be done by now."),
            l("Go to meet him, and return me the package, it's an easy task."),
            l("You will be able to find Don's house in the west of this city, it's the first house just after left bridge, you can't miss it!"),
            l("A young apprentice called Chelios might be waiting outside, speak to him, and return the present to me.");
            emotion 3;

        setq ArtisQuests_Enora, 1;

        closedialog;
        goodbye;
        close;
    }

    function enora_second_quest {
        speech 5,
            l("Thank you for this commission, Chelios is as good as Don when it comes to forging metal."),
            l("By the way, did you ever talk with Don?"),
            l("If so, I hope that he didn't give you any hard time, as once he get up on the wrong side of the bed he transforms himself into an embittered and grumpy old man..."),
            l("Poor Chelios, I don't envy him..."),
            l("So, everything went fine?");

        switch (select (l("Chelios managed to do an excellent work."),
                        l("The old man seemed to be senile.")))
        {
            case 1:
                speech 5, l("Fine, let's see his work...");
                narrator 4, l("Enora is carefully inspecting the sword.");
                speech 4,
                    l("Excellent!"),
                    l("That was a good choice to ask him to forge this sword!");

                break;
            case 2:
                speech 5,
                    l("He may not have all of his wits but keep in mind one thing:");
                    l("Don is renowned throughout the archipelagos for his forgery here on Artis."),
                    l("It's a great honnor for us at the Legion to hold the security of this commerce!");

                break;
        }

        speech 4,
            l("After this small overview of Artis, what do you think of our city?"),
            l("The Legion of Aemil is in charge of the security of the intramural part of most city throughout the archipelagos."),
            l("But we are quite lucky here, outside of some tipsy travelers there are no big problem around here, it's not like capital, Esperia."),
            l("Now that I think about it, I have another task for you. I asked Resa from the light armor shop some new clothes, she is a bit far from here, I can't go there as I need to watch the dock."),
            l("Her shop is on the west part of the city, between the legion building and the library.");

        setq ArtisQuests_Enora, 5;

        closedialog;
        npctalk3 l("Thank you very much!");
        close;
    }

    function enora_third_quest {
        speech 5,
            l("Look how splendid this @@ is!", getitemlink (ArtisTankTop, CamelCottonDye)),
            l("It's nearly as good as one from Esperia, have you ever been there?"),
            l("Ah... Sorry, I forget, again."),
            l("I mean, you forget... Well."),
            l("If you ever find the time, pass by Esperia, it's the capital city of the archipelagos!"),
            l("You can find a chapter of the Legion of Aemil there, like in any other city, but Esperia is important for us as our headquarter is there.");


        do
        {
            .@q = select (l("Do you still need help?"),
                          l("What is this \"legion\"?"));
            switch (.@q)
            {
                case 1:
                    break;
                case 2:
                    enora_legion;
                    break;
            }
        } while (.@q == 2);

        speech 5,
            l("Oh yes, I have a last errand for you, Q'Pid in the market sells some potions."),
            l("It does some days now that I asked her a box of @@, but she didn't deliver anything.", getitemlink (PibberiesInfusion)),
            l("Please try to figure what went wrong with this order and bring me these potions."),
            l("You will be able to find her on the market on the south-east of Artis.");

        setq ArtisQuests_Enora, 7;

        closedialog;
        npctalk3 l("Thank you very much!");
        close;
    }

    function enora_fourth_quest {
        speech 5,
            l("Awesome!"),
            l("I have one more task for you, Trozz, another member of the Legion, sent me a letter earlier today."),
            l("Some citizens are worried about the growing number of Fluffies on the hill of Artis."),
            l("Childeren play there; and can be badly hurt if they play near a Fluffy's nest."),
            l("It would have been easy for me to handle it, but you can earn respect by killing Fluffies in place of their natural-predator!"),
            l("Besides, I need to be here to watch for thieves while La Johanne is docked. You're looking confident so take these!");

        set #ArtisFluffyKilled, 0;

        getitem TrainingGladius, 1;
        getitem LegionTrainingShirt, 1;
        getitem PibberiesInfusion, 5;

        speech 4,
            l("Do you know where the hill is?");

        switch (select (l("Yes I do."),
                        l("Please guide me.")))
        {
            case 1:
                speech 5,
                    l("Perfect! I will wait you here.");
                break;
            case 2:
                speech 5,
                    l("Follow up-stream, and cross the river twice with the two bridges on the north."),
                    l("The hill is located on the north-east of Artis.");
                break;
        }

//      setq ArtisQuests_Enora, 10;

        closedialog;
        npctalk3 l("Thank you very much!");
        close;
    }

    function enora_hill_cleaned {
        if ($FLUFFY_KILLED < 7)
        {
            speech 5,
                l("Oh no, you still have @@ Fluffies to kill.", 8 - $FLUFFY_KILLED);

            return;
        }
        else if ($FLUFFY_KILLED < 8)
        {
            speech 5,
                l("Only one more Fluffy to kill and it's done!");

            return;
        }

        speech 5,
            l("I cannot find the word to express my gratitude for your hard work."),
            l("Keep the sword and the jacket, you deserved them!"),
            l("You also earned the right to enter the Legion building."),
            l("We offer different kind of training sessions and we have a board with open tasks for the city and its surrounding."),
            l("It's a very good place if you want to make some money.");
            emotion 6;

        return;
    }

    function enora_quest_complete {
        switch (getq(ArtisQuests_Enora))
        {
            case 4:
                enora_second_quest;
                break;
            case 6:
                enora_third_quest;
                break;
            case 9:
                enora_fourth_quest;
                break;
            default:
                break;
        }

        return;
    }

    if (getq(ArtisQuests_Enora) == 0)
    {
        speech 4,
            l("Hey! You must be @@.", strcharinfo(0)),
            l("Julia has told me how they found you at sea, on a raft with a logo of--"),
            l("I mean LOG! Made of log!"),
            l("I also hear you have lost all your memories? That's a shame."),
            l("I'm sure you would have some interesting stories to tell!");
    }
    else
    {
        speech 4,
            l("Hey @@!", strcharinfo(0)),
            l("What brings you here today?");
    }

    do
    {
        .@q = getq(ArtisQuests_Enora);
        menuint
            rif(.@q == 0, l("She told me that you had some tasks for me.")), 0,
            rif(.@q == 4 || .@q == 6 || .@q == 9, l("I have your package.")), 1,
            rif(.@q == 10 && #ArtisFluffyKilled >= 10, l("I cleaned up the hill.")), 2,
            rif(.@q >= 1, l("Who is Don?")), 3,
            rif(.@q >= 5, l("Where is the light armor shop?")), 4,
            rif(.@q >= 7, l("Where is the market?")), 5,
            rif(.@q >= 10, l("Where is the hill?")), 6,
            rif(.@q >= 1, l("What is this \"legion\"?")), 7,
            rif(.@q >= 10, l("How many Fluffies did I kill on the hill?")), 8,
            l("I wish I could remember something..."), 9,
            rif(.@q != 0, l("Nothing.")), 10;

        switch (@menuret)
        {
            case 0:
                enora_first_quest;
                break;
            case 1:
                enora_quest_complete;
                break;
            case 2:
                enora_hill_cleaned;
                break;
            case 3:
                enora_don;
            case 4:
                enora_light_armor;
                break;
            case 5:
                enora_market;
                break;
            case 6:
                enora_hill;
                break;
            case 7:
                enora_legion;
                break;
            case 8:
                speech 5,
                    l("You killed @@ Fluffies.", #ArtisFluffyKilled);
                if (#ArtisFluffyKilled > 8)
                {
                    mesq l("If you continue there will be none left!");
                    next;
                }
                break;
            case 9:
                enora_memories;
                break;
            case 10:
                speech 5,
                    l("Please come back anytime!");
                break;
        }
    } while (@menuret != 10);

    closedialog;
    goodbye;
    close;

OnInit:
    .sex = G_FEMALE;
    .distance = 5;
    end;
}

