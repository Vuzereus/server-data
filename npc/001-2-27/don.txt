// Evol scripts.
// Author:
//    Reid
// Description:
//    Don the blacksmith of Artis

001-2-27,35,29,0	script	Don#001-2-27	NPC_HUMAN_MALE_OLD,{

    function improve_equipment {
        speech 5,
            l("Different ways, each part of your equipment can be generally upgraded."),
            l("You have a level for each of your gear, by default when you buy or craft a piece, the level is set to 1."),
            l("You can also improve your equipment in a totally different way with the use of cards.");
        return;
    }

    function card_explanation {
        speech 5,
            l("There are two different kinds of card, the first changes the style of your clothes, the second changes their stats."),
            l("I am not an expert of the first kind, but I know what I am talking about for the second."),
            l("A stat card works on different pieces of equipment."),
            l("Each equipment has a predefined slot number."),
            l("Each cards improve your gear by a ratio or a fixed number on a predefined stat."),
            l("Like, a defensive mythril card can be used on any mythril equipment, and it will improve of 5% the defense of the latter."),
            l("You just have to select it, you choose on which piece of equipment you want to use it, and you are done.");
        return;
    }

    function take_apprentice {
        speech 5,
            l("I do not.");
        emotion 11;

        select (l("What about Chelios?"));

        speech 5,
            l("Chelios was stubborn in his youth, he could not stop to annoy me with his questions while I was working at the forge."),
            l("He did not change while growing up, I repeatedly asked him to leave the forge but in the end he installed himself in front of it..."),
            l("He is mature and he rarely ask questions so I am fine. I can not stand the sight of a whinger anyway.");
        return;
    }

    function good_blacksmith {
        speech 5,
            l("I do not like to brag about it but you will not find any better blacksmith on the whole island."),
            l("If you need somebody to craft a weapon or a plate from a diagrams I am the one that you need.");

        switch (select (l("Can I craft them myself?"),
                        l("Ok.")))
        {
            case 1:
                speech 5,
                    l("You can try, but your chance of success is lesser than a well trained smith and master craftsman.");
                break;
            case 2:
                break;
        }

        return;
    }

    speech 4,
        l("Hi, what do you want kiddo?");

    do
    {
        switch (.@q = select (l("How can I improve my equipment?"),
                              l("What is a card?"),
                              l("Are you a good blacksmith?"),
                              l("Do you take apprentices?"),
                              l("Nothing.")))
        {
            case 1:
                improve_equipment;
                break;
            case 2:
                card_explanation;
                break;
            case 3:
                good_blacksmith;
                break;
            case 4:
                take_apprentice;
                break;
            case 5:
                break;
        }
    } while (.@q != 5);

    goodbye;
    close;

OnInit:
    .sex = G_MALE;
    .distance = 2;
    end;
}

