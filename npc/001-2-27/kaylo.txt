// Evol scripts.
// Author:
//    Reid
// Description:
//    Kaylo is the Shield seller in Don's Blacksmith house on Artis.

001-2-27,27,28,0	script	Kaylo#001-2-27	NPC_KAYLO,{
    speech 4,
        l("Hi customer! What do you want today?");

    switch (select (
        l("Let's make a trade."),
        l("What's wrong with your clothes?"),
        l("What is this place?"),
        l("See you.")))
    {
        case 1:
            closedialog;
            shop "Store#Shield001-2-27";
            close;
        case 2:
            speech 5,
                l("I used a card to improve my clothes."),
                l("There're two different kinds of card, the first changes the style of your clothes, the second changes their stats."),
                l("You should discuss with Don and Calypsan if you want to learn more about it, they are master on this area!");
            break;
        case 3:
            speech 5,
                l("You are on the Blacksmith's house, by Jove!"),
                l("Don is a renowned blacksmith throughout Andorra it's crazy that you don't know about him.");
            break;
        case 4:
            break;
    }

    goodbye;
    close;

OnInit:
    .sex = G_MALE;
    .distance = 4;
    end;
}

