// Evol Script
// Author:
//  Jesusalva
// Description:
//  Controls the boss on 008-4-1 and the Master Book Learning
// see npc/items/master_skillbook.txt for explanation about variables

008-4-1,0,0,0	script	#BossCtrl_008-4-1	NPC_HIDDEN,{
    end;

// Test server: 15 minutes only
OnTimer900000:
    if (!debug)
        end;

// Otherwise, respawn every hour
OnTimer3600000:
    stopnpctimer;
OnInit:
    $@MB_00841=0;
    monster "008-4-1", 67, 30, strmobinfo(1, GameBalance), GameBalance, 1, "#BossCtrl_008-4-1::OnBossDeath";
    end;

OnBossDeath:
    initnpctimer;
    // Only the party which defeated the boss can learn the skill
    getmapxy(.@m$, .@x, .@y, 0);
    .@party=getcharid(1);
    if (.@party > 0)
    {
        $@MB_00841=.@party;
        areatimer("008-4-1", .@x-15, .@y-15, .@x+15, .@y+15, 10, "#BossCtrl_008-4-1::OnBossCheck");
        mapannounce "008-4-1", "Boss deafeated by Party: " + getpartyname(.@party), bc_all;
    }
    else
    {
        $@MB_00841=-2;
        areatimer("008-4-1", .@x-15, .@y-15, .@x+15, .@y+15, 10, "#BossCtrl_008-4-1::OnBossCheck");
        addtimer(20, "#BossCtrl_008-4-1::OnBegin");
        mapannounce "008-4-1", "Boss deafeated by: " + strcharinfo(0), bc_all;
    }
    end;

OnBossCheck:
    @mb_BossId=-1;
    // TODO: Check if you really fought or was just lurking
    // Check if party is correct
    if (getcharid(1) != $@MB_00841)
        end;
OnBegin:
    @mb_BossId=GameBalance;
    @mb_SkillId=MG_COLDBOLT;
    @mb_ItemId=MagicFeather; // Placeholder
    @mb_ItemAm=1;
    addtimer(15000, "#BossCtrl_008-4-1::OnFinish");
    end;

OnFinish:
    @mb_BossId=0;
    @mb_SkillId=0;
    @mb_ItemId=0;
    @mb_ItemAm=0;
    end;

}
