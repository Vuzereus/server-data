// Evol scripts.
// Author:
//    Reid
// Description:
//    Doors NPCs.
//    The third value of the .@nard variable has been removed for the beta2, it'll then be re-added for the Aurora release.
// Variable:
//    0 ShipQUests_Nard
//    1 ShipQuests_Julia
// Values:
//    Nard:
//    00   Introduction of the boxes and Nard. This is displayed when the player never spoke to Nard or his box.
//    01   Nard spoke and gave access to the outdoor of the ship.
//    02   Completed the Gugli quest.
//    03   ChefGado Quest accepted.
//    04   ChefGado Quest completed and "Introduction" chapter finalized.
//    05   Reward taken from the box.
//    10   Has not talked to Julia
//    11   Need to see Julia
//    12   Has been registered by Julia

000-2-0,19,28,0	script	AreaNPC#doors1	NPC_HIDDEN,0,0,{

OnTouch:
    .@nard = getq(ShipQuests_Nard);

    if (getq(General_Narrator) > 0)
    {
        warp "000-1", 93, 100;
        close;
    }

    if (.@nard == 4) goto L_GotoNard;
    if (.@nard == 5) goto L_End;
    if (.@nard > 0 && countitem(718) > 0) goto L_Key;

    setcamnpc "Julia";
    mesn "Julia";
    mesq l("The captain has locked the door, you should go see him.");
    next;

    mesn "Narrator";
    mes col(l("Captain Nard is in the room to your right."), 9);
    next;
    restorecam;

    close;

L_Key:
    warp "000-1", 93, 100;
    close;

L_GotoNard:
    setcamnpc "Julia";
    mesn "Julia";
    mesq l("The captain is waiting for you! Hurry up.");
    next;
    restorecam;

    close;

L_End:
    mesn "Narrator";
    mesq col(l("This door is locked."), 9);
    setcamnpc "Julia";
    next;
    mesn "Julia";
    mesq l("We're soon leaving that island, please return to your cabine.");
    next;
    restorecam;
   
    close;
}

000-2-0,33,28,0	script	AreaNPC#doors2	NPC_HIDDEN,0,0,{

OnTouch:
    .@julia = getq(ShipQuests_Julia);
    if (.@julia == 2) goto L_Warp;
    close;

L_Warp:
    warp "000-2-3", 20, 27;
    close;
}
