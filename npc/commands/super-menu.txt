function	script	SuperMenu	{
    do
    {
        if (debug && !is_dev())
        {
            GlobalDebugMenu;
            return;
        }

        clear;
        setnpcdialogtitle l("Super Menu");
        mes l("This menu contains all options available to you, based on your access privileges.");
        mes "";
        mes l("What do you want to access?");
        next;
        select
            l("Scheduled broadcasts"),
            l("MOTD"),
            rif(is_evtc(), l("Event management")),
            l("Debug");

        switch (@menu)
        {
            case 1: StoneBoard 1; break;
            case 2: MOTDConfig 1; break;
            case 3: GlobalEventMenu 1; break;
            case 4: GlobalDebugMenu 1; break;
        }
    } while (1);
}



-	script	@super	32767,{
    end;

OnCall:

    if (!debug && !is_dev())
    {
        dispbottom l("You do not have the required access privileges to use the Super Menu.");
        end;
    }

    SuperMenu;
    closeclientdialog;
    end;

OnInit:
    bindatcmd "super", "@super::OnCall", 0, 99, 0;
    bindatcmd "numa", "@super::OnCall", 0, 99, 0; // alias for those used to TMW's @numa
}

function	script	GrantSuperSkill	{
    .@debug_skill = getskilllv(EVOL_SUPER_MENU);

    if (.@debug_skill > 0 && !debug)
    {
        skill EVOL_SUPER_MENU, 0, 0; // remove debug skill. Not needed (skill tree)
    }

    else if (.@debug_skill < 1 && debug)
    {
        skill EVOL_SUPER_MENU, 1, 0; // give debug skill
    }
    return;
}

