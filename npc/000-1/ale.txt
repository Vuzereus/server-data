// Evol scripts.
// Authors:
//    Qwerty Dragon
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Aquada box.

000-1,55,23,0	script	Ale#000-1	NPC_DEMON_MAN_SITTING,{
    if (getq(getq(General_Narrator)))
    {
        sailortalk;
    }

    .@g = getq(ShipQuests_Gugli);
    if (.@g == 0) goto L_NoTask;

    .@q = getq(ShipQuests_Ale);
    if (.@q == 1) goto L_CallFunc;

    mesn;
    mesq l("Ouch...");
    next;
    mesq l("These heavy boxes are killing me, I don't feel my arms anymore!");
    next;

    menu
        lg("Gugli sent me down here to help you."), -,
        l("What's that?"), L_Plushroom,
        l("See you aboard."), L_Quit;

    mes "";
    mesn;
    mesq l("Really? That's kind of you, I accept your help!");
    next;
    mesq l("Hmm... Let's see, take this one.");
    next;

    inventoryplace 714, 1;

    setq ShipQuests_Ale, 1;
    getitem 714, 1;

    mes "";
    mesn;
    mesq l("I will take care of the other ones don't worry.");

    close;

L_CallFunc:
    sailorfood;

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), L_TaskDone,
        rif(getq(ShipQuests_Ale) == 1 && (getq(ShipQuests_Astapolos) == 0 || getq(ShipQuests_Gulukan) == 0 || getq(ShipQuests_Jalad) == 0 || getq(ShipQuests_QMuller) == 0 || getq(ShipQuests_Tibbo) == 0), l("I'm still busy, I need to find the other sailors.")), -,
        l("What's that purple thing?"), L_Plushroom;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), L_Bottom,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), L_Bottom,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), L_Jalad,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), L_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), L_Bottom,
        l("Nobody."), L_Quit;

L_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him down around the southern area of the island, ask Jalad for more information.");

    close;

L_Jalad:
    setcamnpc "Jalad";

    next;
    mesn;
    mesq l("Look, here he is!");
    next;

    restorecam;

    close;

L_Plushroom:
    mes "";
    mesn;
    mesq l("'That', as you call it, is a @@. There are plenty of 'em on this island!", getitemlink(515));
    next;
    mesq l("It's a kind of mushroom that tastes like a marshmallow and looks like a plush! @@, get it?", getitemlink(515));
    next;
    mesq l("These funny fungi are mushrooming all around this island. Just pick some @@s and have a try.", getitemlink(515));

    close;

L_NoTask:
    mes "";
    mesn;
    mesq l("Ouch... These boxes are so heavy!");

    close;

L_TaskDone:
    mes "";
    mesn;
    mesq l("That's good to hear!");

    close;

L_Quit:
    closedialog;
    close;

OnInit:
    .sex = G_MALE;
    .distance = 2;
    end;
}
