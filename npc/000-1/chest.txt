// Evol scripts.
// Authors:
//    4144
//    Hal9000
//    Reid
// Description:
//    A treasure chest, with... Treasures inside!
// Variable:
//    x   ShipQuests_TreasureChest
// Values:
//    0   Treasure Chest has not been opened yet.
//    1   Treasure Chest has been opened.

000-1,83,70,0	script	#chest	NPC_CHEST,{
    if (getnpcdir ("") != 4)
    {
        setnpcdir 2;
        initnpctimer;
        startnpctimer;
        close;
    }

    .@q = getq(ShipQuests_TreasureChest);
    if (.@q == 0)
    {
        inventoryplace 514, 1;
        setq ShipQuests_TreasureChest, 1;
        Zeny = Zeny + 100;
        getitem 514, 1;
        npctalk3 l("You open the treasure chest.");
    }

    setnpcdir 6;
    initnpctimer;
    startnpctimer;
    close;

OnTimer160:
    stopnpctimer;
    if (getnpcdir ("") == 2) setnpcdir 4;
    if (getnpcdir ("") == 6) setnpcdir 0;
    end;

OnInit:
    setnpcdistance 2;
    end;
}
