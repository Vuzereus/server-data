// Evol scripts.
// Authors:
//    4144
//    gumi
//    Reid
// Description:
//    A box with clothes for new players.

001-2-22,51,37,0	script	Chest#Artis	NPC_CHEST_BIG,2,4,{

    if (.busy == false)
    {
        specialeffect(.dir == 0 ? 24 : 25, AREA, getnpcid(0)); // closed ? opening : closing
        .dir = .dir == 0 ? 2 : 6; // closed ? opening : closing
        .busy = true; // lock until available again
        initnpctimer;
    }
    end;

OnTimer220:
    .dir = .dir == 6 ? 0 : 4; // closing ? closed : open
    end;

OnTimer500:
    .busy = false; // unlock

    if (.dir == 0)
    {
        stopnpctimer; // stop here if the chest is closed
    }
    end;

OnUnTouch:
    if (getareausers(.x - 2, .y - 4, .x + 2, .y + 6) > 0 || .dir == 0)
    {
        end;
    }
OnTimer30000:
    .busy = true;
    .dir = 6; // closing
    specialeffect(25, AREA, getnpcid(0)); // closing
    setnpctimer 0;
OnTouch:
    end;

OnInit:
    .distance = 2;
    end;
}
