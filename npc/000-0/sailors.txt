// Evol scripts.
// Authors:
//    4144
//    Qwerty Dragon
//    Reid
//    Vasily_Makarov
// Description:
//    Starting script of Evol Online.

000-0,23,20,0	script	Sailors	NPC_SAILORS,6,6,{

OnTouch:
    checkclientversion;
    .@lang = requestlang();
    if (.@lang >= 0 && .@lang <= 9) Lang = .@lang;

    mesn "Narrator";
    mesc(l("You are on a raft, adrift in the sea."), 9);
    next;
    mesc(l("With hunger, thirst, and sleep as your only companions, you have the disturbing realization that you can't remember anything of your former life or identity."), 9);
    next;
    mesc(l("All of a sudden, you hear voices from above."), 9);
    next;
    mesc(l("Your body aches, even your hair hurts, and the bright daylight is painful."), 9);
    next;
    mesc(l("But still, you open your eyes and see a large ship before you!"), 9);
    next;
    mesc(l("Aboard stand sailors trying to communicate with you."), 9);
    next;

    setcamnpc "Sailors", -64, -32;
    mesn "Kralog Voice";
    mesq lg("Hey Frenchy!");
    next;

    setcamnpc "Sailors", 0, -32;
    mesn "Human Voice";
    mesq lg("Why Frenchy? It's a Russian!");
    next;

    setcamnpc "Sailors", 144, -80;
    mesn "Tritan Voice";
    mesq lg("What are you guys saying ? It's a Yoiis!");
    next;

    setcamnpc;
    mesn "Raijin Voice";
    mesq lg("You stupid, she's English, look at the shape of her head.", "You stupid, he's English, look at the shape of his head.");
    next;
    mesq l("Hey you! Can you hear us? Are you okay?");
    next;
    restorecam;

    asklanguage(LANG_ON_SEA);

    setcamnpc;
    mes "";
    mesn;
    mesq lg("Oh, she's still alive!", "Oh, he's still alive!");
    next;
    setcamnpc "Sailors", 144, -80;
    mesq lg("This girl needs help, we need to rescue her!", "This guy needs help, we need to rescue him!");
    next;
    restorecam;

    mesn "Narrator";
    mesc(l("The sailors take you aboard their ship."), 9);
    next;
    mesc(l("Click on the NPCs (Non-Player Characters) around you to continue the introduction."), 9);
    next;

    addtimer(15000,"Elmo#sailors::OnSlow");
    warp "000-0-0", 26, 28;

    closeclientdialog;
    close;

OnInit:
    .sex = G_MALE;
    end;
}
