// Evol scripts.
// Authors:
//    4144
//    Qwerty Dragon
//    Reid
//    Vasily_Makarov
// Description:
//    Starting script of Evol Online.

000-0,23,20,0	script	Sailors	405,6,6,{

OnTouch:
    checkclientversion;
    requestlang .@lang;
    if (.@lang >= 0 && .@lang <= 9) Lang = .@lang;

    mesn "Narrator";
    mes col(l("You are on a raft, adrift in the sea."), 9);
    next;
    mes col(l("With hunger, thirst, and sleep as your only companions; you have the disturbing realization that you can't remember anything of your former life or identity."), 9);
    next;
    mes col(l("All of a sudden, you hear voices from above."), 9);
    next;
    mes col(l("Your body aches, even your hair hurts, and the daylight is painful; but still you open your eyes and see a large ship before you!"), 9);
    next;
    mes col(l("Aboard stand sailors trying to communicate with you."), 9);
    next;

    setcamnpc "Sailors", -64, -32;
    mesn "Orc Voice";
    mesq lg("Hey Frenchy!");
    next;

    setcamnpc "Sailors", 0, -32;
    mesn "Elven Voice";
    mesq lg("Why Frenchy? It's a Russian!");
    next;

    setcamnpc "Sailors", 144, -80;
    mesn "Tritan Voice";
    mesq lg("What are you guys saying ? It's a Yoiis!");
    next;

    setcamnpc;
    mesn "Raijin Voice";
    mesq lg("You stupid, she's english, look at the shape of her head.",
            "You stupid, he's english, look at the shape of his head.");
    next;
    mesq l("Hey you! Can you hear us? Are you okay?");
    next;
    restorecam;

    menu
        menuimage("flags/en", "I hear you... (English)"), -,
        menuimage("flags/fr", "Je vous entends... (Français)"), -,
        menuimage("flags/ru", "Да я вас слышу... (Русский)"), -,
        menuimage("flags/es", "Te oigo... (Español)"), -,
        menuimage("flags/pt_BR", "Eu te ouço... (Português)"), -,
        menuimage("flags/de", "Ich höre euch... (Deutsch)"), -,
        menuimage("flags/vls", "'k hoar ui wel... (Vlaams)"), -,
        menuimage("flags/pl", "Słyszę cię... (Polski)"), -,
        menuimage("flags/it", "Vi sento... (Italiano)"), -,
        menuimage("flags/nl_BE", "Ik kan je horen... (Nederlands)"), -,
        menuimage("flags/ca", "Et escolto (Català)"), -;
    .@lang = @menu-1;
    if (.@lang < 0 || .@lang > 10) close;
    Lang = .@lang;

    setcamnpc;
    mes "";
    mesn;
    mesq lg("Oh, she's still alive!",
            "Oh, he's still alive!");
    next;
    setcamnpc "Sailors", 144, -80;
    mesq lg("This girl needs help, we need to rescue her!",
            "This guy needs help, we need to rescue him!");
    next;
    restorecam;

    mesn "Narrator";
    mes col(l("The sailors take you aboard their ship."), 9);
    next;
    mes col(l("Click on the NPCs around you to continue the introduction."), 9);
    next;

    warp "000-0-0.gat", 26, 28;

    closedialog;
    close;

OnInit:
    setnpcsex G_MALE;
    end;
}
