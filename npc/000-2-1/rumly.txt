// Evol scripts.
// Authors:
//    Vasily_Makarov
// Description:
//    Stat resetter.
// Variables:
//    General_Rumly
// Values:
//    0  Player hasn't met Rumly
//    1  Last time player has told that he will never come back
//    2  Last time player has told that he will come back later
// Others:
// .@visited - Rumly actual variable
// .@wasSP - free status points before reset

000-2-1,67,25,0	script	Rumly	NPC_RUMLY,{

    setnpcdir "Rumly", 2;
    stopnpctimer;
    initnpctimer;

    speech 4,
        l("Hello! Do you have any mushrooms?");

L_Menu:
    .@visited = getq(General_Rumly);

    menu
        rif(.@visited == 0, l("Who are you?")), L_FirstTime,
        rif(.@visited >  0, l("Can you reset my stats please?")), L_ResetStats,
        lg("I should go, sorry."), -;

    if (.@visited != 2) goto L_Quit;
        
    speech 5,
        l("See you! And come back with the mushrooms!");

    goto L_Quit;

L_ResetStats:
    if (.@visited == 1) goto L_ChangedMind;
    
    speech 5,
        lg("Are you sure about this?");

L_ConfirmReset:
    menu
        rif(countitem(Plushroom) == 0, lg("Yes, I am sure that I want to reset my stats.")), -,
        rif(countitem(Plushroom) >= 1, lg("Yes, and I have even brought the mushrooms you like!")), -,
        lg("No, I need to think about it a little bit more."), L_Later,
        lg("I've just decided I will never reset my stats, sorry."), L_Never;

L_GiveShrooms:
    speech 5,
        l("Then give me one @@ and we should start!", getitemlink(Plushroom));

    menu
        rif(countitem(Plushroom) >= 1, l("Of course! *give him one @@*", l(getitemname(Plushroom)))), -,
        rif(countitem(Plushroom) == 0, lg("Sorry, I have no any mushrooms with me.")), L_Later,
        lg("No way. I really should go."), L_Later;

    delitem Plushroom, 1;

    speech 5,
        l("Thank you"),
        l("Now stand still. It would not take much time...");

    .@wasSP = StatusPoint;
    resetstatus;
    if (StatusPoint == .@wasSP) goto L_NothingToReset;

    speech 5,
        l("Let's see. @@ of your status points have just been reset.", StatusPoint - .@wasSP),
        l("Spend it wisely this time."),
        lg("But you are welcome to reset your stats again if you will bring me more mushrooms!");

    goto L_Quit;

L_NothingToReset:
    speech 5,
        l("It seems that you have no status points to reset!"),
        lg("But mushrooms you have brought are really awesome you know."),
        lg("Come back when you will really need me. And bring more mushrooms with you!");

    goto L_Quit;

L_ChangedMind:
    speech 5,
        lg("Changed your mind, uh?");

    menu
        lg("Yes, I really need my stats to be reset."), -,
        lg("Sorry, I've just realized that I still do not need my stats reset."), L_Never,
        rif(countitem(Plushroom) >= 1, lg("I have brought some mushrooms to you!")), L_GiveShrooms;

    setq General_Rumly, 2;
    .@visited = 2;

    speech 5,
        l("Very good."),
        l("Status point reset can't be undone. Do you really want this?");

    goto L_ConfirmReset;

L_FirstTime:
    speech 5,
        l("I am Rumly, the one who likes mushrooms so much!"),
        lg("Also I can make your stats clean! Just like fresh mushrooms...");

    menu
        lg("Sounds well! I think I could get you that mushrooms"), -,
        rif(countitem(Plushroom) >= 1, lg("I already have some mushrooms you like so much.")), -,
        lg("I have no time for collecting mushrooms right now. We should talk later."), L_Later,
        lg("I will never need to reset my stats because I am sure I am doing right!"), L_Never;

    setq General_Rumly, 2;

    speech 5,
        l("Great! Can't wait for tasty mushrooms!");

    goto L_Menu;

L_Later:
    setq General_Rumly, 2;

    speech 5,
        l("Come back soon! And bring the mushrooms with you!");

    goto L_Quit;

L_Never:
    setq General_Rumly, 1;

    speech 5,
        lg("I am sure that you will change your mind.");

    goto L_Quit;

L_Quit:
    setnpcdir "Rumly", 4;
    stopnpctimer;
    initnpctimer;

    goodbye;


OnTimer1800:
    stopnpctimer;

    if (getnpcdir ("Rumly") == 2) setnpcdir "Rumly", 6;
    if (getnpcdir ("Rumly") == 4) setnpcdir "Rumly", 8;

    end;

OnInit:
    .sex = G_MALE;
    end;
}
