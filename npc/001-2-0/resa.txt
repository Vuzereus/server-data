// Evol scripts.
// Authors:
//    Reid
// Description:
//    Light Armor shop keeper.
// Variables:
//    ArtisQuests_Enora
// Values:
//    0    Default.
//    1    BlackSmith quest delivered.
//    2    Chelios Quest given.
//    3    Chelios Quest done.
//    4    BlackSmith gave the sword.
//    5    Light Armor Shop quest delivered.
//    6    Light Armor Shop gave the cloths.

001-2-0,37,28,0	script	Resa	NPC_ELVEN_FEMALE_ARMOR_SHOP,{

    function explain_craft {
        speech 5,
            l("Did you see Don the blacksmith? He might knows more about it if you want to improve your metal equipment."),
            l("Nevertheless, you can craft some cards that you can then attach to your equipment."),
            l("We sell a brimmed hat, you can craft a feather card and attach it to this hat to obtain an enhanced version of it!");

        return;
    }

    function enora_quest {
        speech 5,
            l("I thought that she would never come to pick it up!"),
            l("Here it is, a fashionable @@.", getitemlink (ArtisTankTop, CamelCottonDye)),
            l("I asked Calypsan to dye this tank top, now it has an unique look!");

        narrator 4, l("You pick up the package.");

        setq ArtisQuests_Enora, 6;

        return;
    }

    speech 4,
        l("Welcome to my office."),
        l("What would you like today?");

    do
    {
        .@enora = getq (ArtisQuests_Enora);
        menuint rif (.@enora == 5, l("I came to retrieve a package for Enora.")), 0,
                menuaction(l("Trade")), 1,
                l("How can I improve my equipment?"), 2,
                menuaction(l("Quit")), 3;

        switch (@menuret)
        {
            case 0:
                enora_quest;
                closedialog;
                goodbye;
                close;
            case 1:
                closedialog;
                shop "Store#001-2-0";
                close;
            case 2:
                explain_craft;
                break;
            case 3:
                closedialog;
                goodbye;
                close;
        }
    } while (1);


OnInit:
    .sex = G_FEMALE;
    .distance = 2;
    end;
}
