// Evol functions.
// Authors:
//    gumi
//    Reid
// Description:
//    Adds a new save point location.
// Usage:
//    savepointparticle;
//    savepointparticle NO_INN;
//    savepointparticle map, x, y, NO_INN;
// Description:
//    Save location with arguments:
//      getarg(0)   map name,
//      getarg(1)   x's value,
//      getarg(2)   y's value,
//      getarg(3)   INN flag.

function	script	savepointparticle	{

    if (gettimetick(2) - @lastSave < 5)
        return;

    .@mapname$ = getarg(0, "");
    .@mapx = getarg(1, -1);
    .@mapy = getarg(2, -1);
    .@i = 3;

    if (.@mapy < 1)
    {
        .@npc$ = strnpcinfo(0);
        .@mapname$ = getvariableofnpc(.map$, .@npc$);
        .@mapx = getvariableofnpc(.x, .@npc$);
        .@mapy = getvariableofnpc(.y, .@npc$);
        .@i = 0;
    }

    message strcharinfo(0), l("Your position has been saved.");

    if (getarg(.@i, NO_INN) == NO_INN)
    {
        INN_REGISTER = NO_INN;
    }
    savepoint .@mapname$, .@mapx, .@mapy;
    specialeffect(4, SELF, getcharid(3));
    @lastSave = gettimetick(2);

    return;
}
